name := 'glowberry-settings'
appid := 'io.github.hojjatabdollahi.glowberry-settings'

rootdir := ''
prefix := '/usr'

base-dir := absolute_path(clean(rootdir / prefix))
install-dir := base-dir / 'share'

cargo-target-dir := env('CARGO_TARGET_DIR', 'target')
bin-src := cargo-target-dir / 'release' / name
bin-dst := base-dir / 'bin' / name

desktop-src := 'data' / appid + '.desktop'
desktop-dst := install-dir / 'applications' / appid + '.desktop'

scalable-src := 'data' / 'icons' / appid + '.svg'
scalable-dst := install-dir / 'icons' / 'hicolor' / 'scalable' / 'apps' / appid + '.svg'

symbolic-src := 'data' / 'icons' / appid + '-symbolic.svg'
symbolic-dst := install-dir / 'icons' / 'hicolor' / 'symbolic' / 'apps' / appid + '-symbolic.svg'

# Default recipe which runs `just build-release`
default: build-release

# Compiles with debug profile
build-debug *args:
    cargo build {{args}}

# Compiles with release profile
build-release *args: (build-debug '--release' args)

# Installs files
install:
    install -Dm0755 {{bin-src}} {{bin-dst}}
    install -Dm0644 {{desktop-src}} {{desktop-dst}}
    install -Dm0644 {{scalable-src}} {{scalable-dst}}
    install -Dm0644 {{symbolic-src}} {{symbolic-dst}}

# Uninstalls installed files
uninstall:
    rm -f {{bin-dst}}
    rm -f {{desktop-dst}}
    rm -f {{scalable-dst}}
    rm -f {{symbolic-dst}}
